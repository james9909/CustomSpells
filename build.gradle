import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

plugins {
    id 'idea'
    id 'org.jetbrains.kotlin.jvm' version '1.6.10'
    id 'com.github.johnrengelman.shadow' version '4.0.2'
}

wrapper {
    gradleVersion = "6.5"
    setDistributionType(Wrapper.DistributionType.ALL)
}

group 'com.github.james9909'
version '1.19.2-0.1.0'

sourceCompatibility = 17
targetCompatibility = 17

// Disable the regular jar task, since we only want the shadow jar
jar.enabled = false
tasks.build.dependsOn tasks.shadowJar

shadowJar {
    minimize()
    relocate("kotlin", "com.github.james9909.enderswap.shaded.kotlin")

    exclude "META-INF/**"
}

tasks.withType(Jar) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://repo.papermc.io/repository/maven-public/" }
    maven { url = "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url = "https://jitpack.io" }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compileOnly "io.papermc.paper:paper-api:1.19.3-R0.1-SNAPSHOT"
    testImplementation "org.junit.jupiter:junit-jupiter:5.4.2"
    testImplementation "com.github.seeseemelk:MockBukkit-v1.19:2.29.0"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

test {
    outputs.upToDateWhen {false}
    useJUnitPlatform()
    testLogging {
        afterTest { desc, result ->
            logger.quiet "${desc.className} > ${desc.name}: ${result.resultType}"
        }
        // Taken from https://stackoverflow.com/a/36130467
        afterSuite { desc, result ->
            if (!desc.parent) {
                def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                def startItem = '|  ', endItem = '  |'
                def repeatLength = startItem.length() + output.length() + endItem.length()
                println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
            }
        }
    }
}
